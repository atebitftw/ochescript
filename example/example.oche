// Here we are including a library script called "dice".
// It's up to the dart code to handle the include paths.
#include dice

// You don't see this, but it is injected into the script, 
// just prior to runtime, as a global variable.
// var globalStuff = 25.0;

fun say(str) {
   print "(OcheScript) $str";
}

fun rollDice() {
   say("Accessing dice library to roll a D20.\n");

   var roll = Dice().rollD20();

   switch(roll){
      case 20:
         say("You rolled a natural 20! Lucky!\n");
         break;
      case 1:
         say("You rolled a 1!  Fumble!\n");
         break;
      default:
         say("You rolled: $roll.\n");
         break;
   }
}

// We don't have to name our entry function "main", but it is good practice.
async fun main() {
   rollDice();

   say("Calling native function clock() to get elapsed milliseconds since epoch.");
   var start = clock();

   say("Got globalStuff with value: $globalStuff.");

   // Here we are mutating the variable that (we assume) was passed in from the Dart code at compile time.
   globalStuff = globalStuff.sqrt().round();

   say("Used native method .sqrt() on globalStuff: $globalStuff.");

   say("Calling dart() function 'double_me'. One moment please...");

   var callClock = clock();

   var result = await dart("double_me", [globalStuff]);
   
   say("Returned from dart call with result ${result}.");

   // String interpolation of expressions...
   say("dart() call took about ${((clock() - callClock) / 1000).round()} seconds.");

   say("Calling out() function with result.");
   
   out("result", result);
   
   var elapsed = clock() - start;
   
   say("Script elapsed time: ${elapsed} ms.");

   out("elapsed", elapsed);
}

// await is not strictly necessary here since we are calling an entry function that does not return a value, but it is good practice.
await main();

