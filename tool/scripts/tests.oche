#include unit_testing
#include models
//361

fun main() {
  print("OcheScript Unit Tests");
  print("-------------------------");
  var start = clock();

  group("Logical Operators", fun() {
    test("OR", fun() {
      expect(true || false, isTrue());
      expect(false || true, isTrue());
      expect(false || false, isFalse());
    });

    test("AND", fun() {
      expect(true && !false, isTrue());
      expect(true && false, isFalse());
    });

    test("NOT", fun() {
      expect(!false, isTrue());
    });
  });

  group("Comparison", fun() {
    test("Greater Equal", fun() {
      expect(42 >= 43, isFalse());
      expect(42 >= 42, isTrue());
    });

    test("Greater", fun() {
      expect(42 > 43, isFalse());
      expect(42 > 42, isFalse());
    });

    test("Less", fun() {
      expect(42 < 43, isTrue());
      expect(42 < 42, isFalse());
      expect(43 < 42, isFalse());
    });

    test("Less Equal", fun() {
      expect(42 <= 43, isTrue());
      expect(42 <= 42, isTrue());
      expect(43 <= 42, isFalse());
    });

    test("Equality", fun() {
      expect(42 == 43, isFalse());
      expect(42 == 42, isTrue());
    });

    test("Not Equal", fun() {
      expect(42 != 42, isFalse());
      expect(42 != 43, isTrue());
    });
  });

  group("Control Flow", fun() {
    test("While Simple", fun() {
      var i = 0;
      while (i < 10) {
        i = i + 1;
      }
      expect(i, equals(10));
    });

    test("While Nested", fun() {
      var x = 0;
      var i = 0;
      while(x < 10){
        x = x + 1;
        var y = 0;
        while (y < 10) {
          y = y + 1;
        }
        i = i + y;
      }
      expect(i == 100 && x == 10, isTrue());
    });

    test("For Loop Simple", fun() {
      var t = 0;
      for(var i = 0; i < 3; i = i + 1){
        t = t + 1;
      }
      expect(t, equals(3));
    });

    test("For Loop Nested", fun() {
      var t = 0;
      for(var i = 0; i < 3; i = i + 1){
        for(var x = 0; x < 3; x = x + 1){
          t = t + 1;
        }
      }
      expect(t, equals(9));
    });

    test("Break", fun() {
      var t = 0;
      var i = 0;
      while(i < 10) {
        if (i == 5) {
          break;
        } else {
          t = t + 1;
        }
        i = i + 1;
      }
      var firstResult = t == 5;

      t = 0;
      for (i = 0; i < 10; i = i + 1){
        if (i == 5) {
          break;
        } else {
          t = t + 1;
        }
      }
      var secondResult = t == 5;
      expect(firstResult && secondResult, isTrue());
    });

    test("Continue", fun() {
      var t = 0;
      var i = 0;
      while(i < 10) {
        if (i >= 5 && i <= 6) {
          i = i + 1;
          continue;
        } else {
          t = t + 1;
        }
        i = i + 1;
      }
      var firstResult = t == 8;

      t = 0;
      for (i = 0; i < 10; i = i + 1){
        if (i >=5 && i <= 6) {  
          i = i + 1;
          continue;
        } else {
          t = t + 1;
        }
      }
      var secondResult = t == 8;
      expect(firstResult && secondResult, isTrue());
    });
    
    test("Switch", fun() {
       var result = "";
       var fruit = "apple";
       switch(fruit) {
         case "apple":
           result = "apple";
           break;
         case "banana":
           result = "banana";
           break;
         default:
           result = "default";
       }
       expect(result, equals("apple"));
       
       var a = 1;
       switch(a) {
        case 0:
          result = "zero";
          break;
        case 1:
          result = "one";
          break;
        default:
          result = "default";
      }
      expect(result, equals("one"));
      
      a = 2;
      switch(a) {
        case 0:
          result = "zero";
          break;
        case 1:
          result = "one";
          break;
        default:
          result = "default";
      }
      expect(result, equals("default"));
      
      // Fallthrough
      a = 0;
      var count = 0;
      switch(a) {
        case 0:
          count = count + 1;
        case 1:
          count = count + 1;
          break;
        case 2:
          count = count + 1;
      }
      expect(count, equals(2));
    });
  });

  group("Scope", fun() {
    test("Block Scope", fun() {
      var initialized = 0;
      var incremented = 0;
      {
        var a = 1;
        var b = 2;
        {
          var c = 3;
          var d = 4;
          {
            var e = 5;
            var f = 6;
            initialized = a == 1 && b == 2 && c == 3 && d == 4 && e == 5 && f == 6;
            a = a + 1;
            b = b + 1;
            c = c + 1;
            d = d + 1;
            e = e + 1;
            f = f + 1;
            incremented = a == 2 && b == 3 && c == 4 && d == 5 && e == 6 && f == 7;
          }
        }
      }
      expect(initialized && incremented, isTrue());
    });
  });

  group("Collections", fun() {
    test("Lists", fun() {
      var l = [1, 2, 3];
      expect(l[0], equals(1));
      expect(l[1], equals(2));
      expect(l[2], equals(3));

      l[0] = 10;
      expect(l[0], equals(10));

      var nested = [[1, 2], [3, 4]];
      expect(nested[0][1], equals(2));
      expect(nested[1][0], equals(3));

      var nextList = ["hi", false, 42];
      expect(nextList[0], equals("hi"));
      expect(nextList[1], equals(false));
      expect(nextList[2], equals(42));
    });

    test("Maps", fun() {
      var m = { "one": 1, "two": 2 };
      expect(m["one"], equals(1));
      expect(m["two"], equals(2));
      m["three"] = 3;
      expect(m["three"], equals(3));
      
      var nested = { "inner": { "val": 42 } };
      expect(nested["inner"]["val"], equals(42));
      
      var foo = { "bar" : [1, 2, 3] };
      expect(foo["bar"][0], equals(1));

      expect(m.keys().contains("two"), isTrue());
      expect(m.values().contains(2), isTrue());
    });

    test("Helpers", fun() {
      var foo = [1, 2, 3];
      expect(foo.length(), equals(3));

      var acc = foo.fold(0, fun(x, y) { return x + y; });
      expect(acc, equals(6));

      var bar = { "one": 1, "two": 2 };
      expect(bar.length(), equals(2));
      expect(bar.containsKey("one"), isTrue());
      expect(bar.containsKey("two"), isTrue());
      expect(bar.containsKey("three"), isFalse());
    });
  });

  group("Strings", fun() {
    test("Concat", fun() {
      expect("hello" + " " + "world" == "hello world", isTrue());
      expect("hello" + " " + [1, 2, false] + " world" == "hello [1, 2, false] world", isTrue());
      var theStringTest = "hello" + " " + { "one": 1, "two": 2 } + " world";
      expect(theStringTest.contains("hello {") && theStringTest.contains("two: 2") && theStringTest.contains("} world"), isTrue());
    });

    test("Substring", fun() {
      expect("hello".substring(0, 3) == "hel", isTrue());
      expect("hello".substring(3, 5) == "lo", isTrue());
      expect("hello".substring(0, 5) == "hello", isTrue());
      expect("hello".substring(0, 1) == "h", isTrue());
    });
    
    test("Split", fun() {
      expect("".split("l").toString(), equals([].toString()));
      expect("a,b,c,d,e,f,g,".split(",").toString(), equals(["a", "b", "c", "d", "e", "f", "g", ""].toString()));
      expect("hello".split("l").toString(), equals(["he", "", "o"].toString()));
      expect("hello".split("h").toString(), equals(["","ello"].toString()));
      expect("hello".split("o").toString(), equals(["hell",""].toString()));
    });
    
    test("Length", fun() {
      var foo = "forty two";
      expect(foo.length(), equals(9));
      expect("".length(), equals(0));
      expect([].length(), equals(0));
      expect({}.length(), equals(0));
      expect("hello".length(), equals(5));
      expect([1, 2, 3].length(), equals(3));
      expect({"one": 1, "two": 2}.length(), equals(2));
      expect([1, 2, 3].length() + "hello".length(), equals(8));
    });
    test("Interpolation", fun() {
      var name = "World";
      expect("Hello $name!", equals("Hello World!"));

      var a = 10;
      var b = 20;
      expect("Sum: ${a + b}", equals("Sum: 30"));

      var item = "apple";
      var count = 5;
      expect("I have $count ${item}s.", equals("I have 5 apples."));
      
      expect("Outer ${ "Inner" } Space", equals("Outer Inner Space"));

      var name2 = "Inception";
      expect("Level ${ "Deep: $name2" }", equals("Level Deep: Inception"));

      expect("Cost: \$100", equals("Cost: \$100"));

      // Complex mixed types
      var result = "Bool: ${true}, Num: ${1.5}, List: ${[1,2]}";
      expect(result.contains("Bool: true"), isTrue());
      expect(result.contains("Num: 1.5"), isTrue());
    });
  });

  group("Math", fun() {
    test("modulo inline precedence", fun() {
      expect(3 + 3 * 10 % 4 + 5, equals(10));
    });
    test("postfix and prefix increment and decrement", fun() {
      var a = 1;
      expect(a++, equals(1));
      expect(a, equals(2));
      expect(++a, equals(3));
      expect(a, equals(3));
      expect(a--, equals(3));
      expect(a, equals(2));
      expect(--a, equals(1));
      expect(a, equals(1));
    });
    test("Functions", fun() {
      expect(10 % 3, equals(1));
      expect(1.5.round(), equals(2));
      expect(1.5.ceil(), equals(2));
      expect(1.5.floor(), equals(1));
      expect(-1.abs(), equals(1));
      expect(0.acos(), equals(3.141592653589793 / 2));
      expect(0.asin(), equals(0));
      expect(4.sqrt(), equals(2));
      expect(2.pow(3), equals(8));
      expect(10.log(), equals(2.302585092994046));
      expect(1.exp(), equals(2.718281828459045));
      expect((3.141592653589793 / 2).sin(), equals(1));
      expect((3.141592653589793 / 2).cos(), lessThan(1));
      expect((1).atan(), equals(3.141592653589793 / 4));
      expect((1).atan2(1), equals(3.141592653589793 / 4));
      expect((1).max(2), equals(2));
      expect((1).min(2), equals(1));
    });
  });

  group("Recursion", fun() {
    test("Simple Recursion", fun() {
       fun doRecursion(value) {
          if (value == 0) {
            return true;
          }
          expect(value, greaterThan(0));
          return doRecursion(value - 1);
        }
        expect(doRecursion(5), isTrue());
    });
  });

  group("is operator", fun() {
    test("Simple", fun() {
      expect(1 is String, isFalse());
      expect(1 is Num, isTrue());
      expect(1 is Bool, isFalse());
    });

    test("Comprehensive", fun() {
      var n = 123;
      expect(n is Num, isTrue());
      expect(n is Bool, isFalse());
      expect(n is String, isFalse());

      var b = true;
      expect(b is Bool, isTrue());
      expect(b is Num, isFalse());

      var s = "hello";
      expect(s is String, isTrue());
      expect(s is Num, isFalse());

      var l = [1, 2, 3];
      expect(l is List, isTrue());
      expect(l is Map, isFalse());

      var m = {"a": 1};
      expect(m is Map, isTrue());
      expect(m is List, isFalse());

      var d = now();
      expect(d is Date, isTrue());
      expect(d is Duration, isFalse());

      var dur = now() - d;
      expect(dur is Duration, isTrue());
      expect(dur is Date, isFalse());
    });

    test("Classes And Inheritance", fun() {
      class Animal { }

      class Dog extends Animal { }

      class Yorkie extends Dog { }

      class Cat extends Animal { }

      var animal = Animal();
      var dog = Dog();
      var cat = Cat();
      var yorkie = Yorkie();
      expect(animal is Animal, isTrue());
      expect(dog is Dog, isTrue());
      expect(dog is Animal, isTrue());
      expect(dog is Cat, isFalse());
      expect(cat is Animal, isTrue());
      expect(cat is Dog, isFalse());
      expect(dog is Yorkie, isFalse());
      expect(cat is Yorkie, isFalse());
      expect(yorkie is Yorkie, isTrue());
      expect(yorkie is Dog, isTrue());
      expect(yorkie is Animal, isTrue());
      expect(yorkie is Cat, isFalse());
    });
   
  });

  group("Models", fun() {
    test("ModelBase", fun() {
      var model = ModelBase();
      expect(model is ModelBase, isTrue());
    });

    test("ModelBase JSON", fun() {
      var model = ModelBase();
      model.foo = "42 is cool";
      var json = model.toJSON();  
      expect(json is String, isTrue());
      expect(json.contains("42 is cool"), isTrue());
    });

    test("ModelBase serialize/deserialize", fun() {
      var model = ModelBase();
      model.foo = "42 is cool";
      var json = model.toJSON();
      var model2 = ModelBase().fromJSON(json);
      expect(model2.foo, equals("42 is cool"));
    });
    
    test("ModelBase serialize/deserialize with nested models", fun() {
      var model = ModelBase();
      model.foo = "42 is cool";

      class NestedModel extends ModelBase { 
        init(){
            super.init();
            this.modelName = "nested_model";
        }
      }
      registerModel(NestedModel);

      model.bar = getModel("nested_model")[0];
      model.bar.blorp = 42;
      model.bar.createdBy = "test";
      var json = model.toJSON();  
      var model2 = ModelBase().fromJSON(json);
      expect(model2.toString(), equals(model.toString()));
      expect(model2.foo, equals("42 is cool"));
      expect(model2.bar is NestedModel, isTrue());
      expect(model2.bar.blorp, equals(42));
      expect(model2.bar.createdBy, equals("test"));
      expect(model.id.length(), equals(20));
      expect(model2.bar.id.length(), equals(20));
      expect(model.id != model2.bar.id, isTrue());
    });
    
    test("registered model is returned", fun() {
      class Bear extends ModelBase { 
        init(){
            super.init();
            this.modelName = "bear";
        }
      }
      registerModel(Bear);
      var result = getModel("bear");
      expect(result.head().modelName, equals("bear"));

      result = getModel("cat");
      expect(result.isEmpty(), isTrue());
    });
  });

  group("Futures", fun() {
    test("wait", async fun() {
      var result = await wait(1000);
      expect(result, equals(1000));
    });

    // test("awaitAllFutures", async fun() {
    //   var future1 = fun() { return delayed(1000).then(fun(){ return 1;});};
    //   var future2 = fun() { return delayed(2000).then(fun(){ return 2;});};
    //   var future3 = fun() { return delayed(3000).then(fun(){ return 3;});};
    //   var result = await awaitAllFutures([future1(), future2(), future3()]);
    //   print(result);
    //   expect(result.length(), equals(3));
    //   expect(result[0], equals(1));
    //   expect(result[1], equals(2));
    //   expect(result[2], equals(3));
    // });
  });
  
  group("System", fun() {
    test("Clock", fun() {
      var start = clock();
      for(var i = 0; i < 100000; i = i + 1){ }
      var end = clock();
      expect(end > start, isTrue());
      var expression = clock() + clock();
      expect(expression > 0, isTrue());
    });
    
    test("Dart Interop", async fun() {
      var result = await dart("foo", ["hello"]);
      expect(result[0] == "hello", isTrue());
    });
    
    test("Out", fun() {
      out("int_out", 42);
      out("string_out", "hello");
      out("bool_out", false);
      out("map_out_test", {"foo": false, "bar": true});
      out("list_out_test", [1, 2, 3]);
    });
  });

  var end = clock();
  testSummary();
  print("Total time: " + (end - start) + "ms");
}

main();